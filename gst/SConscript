# vim: syntax=python
cythonBuilder = Builder(action = 'cython $SOURCE --cplus',
                        suffix = '.cpp',
                        src_suffix = '.pyx')

env = Environment(BUILDERS={'Py2C':cythonBuilder},
                  SCONS_CXX_STANDARD="c++11"
                 )

# note implicit concatenation -- make sure to leave spaces at the ends!
env.MergeFlags([
    '!pkg-config --cflags --libs '
        'gstreamer-1.0 ',
    '!python3-config --cflags --libs',
    '-std=c++1y',
    '-Wfatal-errors',
    '-g',
    '-Wl,--no-as-needed',
    '-lasound',
    '-lpthread',
    '-lrtmidi'])

def cythonPseudoBuilder(env,cysource,others):
    cppCode = env.Py2C(cysource)
    o=env.SharedObject(others)
    env.SharedLibrary(cysource,[o,cppCode],LIBPREFIX='')

env.AddMethod(cythonPseudoBuilder,'Cython')
o=["midi.cpp","sampler.cpp","chan.cpp"]
c=env.Cython('pysampler',o)
m=env.Program(["main.cpp"]+o)

